apply plugin: "fabric-loom"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

project.archivesBaseName = project.silk_archive
project.version = project.silk_version
project.group = project.silk_group

loom {
	runConfigs.configureEach {
		ideConfigGenerated = true
	}
}

repositories {
	maven {
		url = "https://repo.codemc.io/repository/nms/"
	}

	maven {
		name = "Jitpack"
		url 'https://jitpack.io/'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:1.18.2"
	mappings loom.officialMojangMappings()

	implementation project(":minecraft")

	// Required for mixin annotation processor
	annotationProcessor "org.ow2.asm:asm:${project.asm_version}"
	annotationProcessor "org.ow2.asm:asm-analysis:${project.asm_version}"
	annotationProcessor "org.ow2.asm:asm-commons:${project.asm_version}"
	annotationProcessor "org.ow2.asm:asm-tree:${project.asm_version}"
	annotationProcessor "org.ow2.asm:asm-util:${project.asm_version}"

	/**
	 * Ensure we are using the mixin version loader is built against to test the AP.
	 * Otherwise Loom will default to an older version (due to no mod loader on the mod* configs)
	 */
	annotationProcessor ("net.fabricmc:sponge-mixin:${project.mixin_version}") {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
	}

	// Fabric-ASM (MM)
	modImplementation "com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}"
	include "com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}"

	// Silk: For developing.
	implementation "org.spigotmc:spigot:1.18.2-R0.1-20220416.071816-3:remapped-mojang"

//	modImplementation "net.fabricmc.fabric-api:fabric-api:0.51.1+1.18.2"
	// Silk end.
}

// Silk: remove minecraft provided by loom.
configurations.all {
	exclude module: 'minecraft-merged-named'
}

sourceSets {
	main {
		java.srcDirs = ['src/main/java']
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}